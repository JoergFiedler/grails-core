dependencies {

    testRuntime 'javax.servlet.jsp:jsp-api:2.1'
    testRuntime 'jstl:jstl:1.1.2'
    testRuntime 'javax.el:el-api:1.0'

    testCompile "commons-io:commons-io:${commonsIOVersion}"
    testCompile project(':grails-test-suite-base'),
                project(':grails-plugin-testing'),
                project(':grails-plugin-domain-class'),
                project(':grails-plugin-codecs'),
                project(':grails-plugin-datasource'),
                project(':grails-plugin-i18n'),
                project(':grails-plugin-servlets'),
                project(':grails-plugin-async'),
                project(':grails-plugin-log4j'),
                project(':grails-plugin-url-mappings'),
                project(':grails-plugin-services'),
                project(':grails-web')

    testCompile "org.grails:grails-datastore-gorm-hibernate:$datastoreVersion", {
        exclude group: 'org.grails', module:'grails-datastore-gorm'
        exclude group: 'org.grails', module:'grails-datastore-core'
        exclude group: 'org.springframework', module:'spring-core'
        exclude group: 'org.springframework', module:'spring-context'
        exclude group: 'org.springframework', module:'spring-web'
        exclude group: 'org.springframework', module:'spring-beans'
        exclude group: 'org.springframework', module:'spring-tx'
        exclude group: 'org.slf4j', module:'jcl-over-slf4j'
        exclude group: 'org.slf4j', module:'jul-to-slf4j'
        exclude group: 'org.slf4j', module:'slf4j-api'
        exclude group: 'org.slf4j', module:'slf4j-simple'
        exclude group: 'org.grails', module:'grails-bootstrap'
        exclude group: 'org.grails', module:'grails-plugin-domain-class'
        exclude group: 'org.grails', module:'grails-core'
        exclude group: 'org.grails', module:'grails-web'
        exclude group: 'org.grails', module:'grails-test'
        exclude group: 'org.grails', module:'grails-async'
        exclude group: 'commons-collections', module:'commons-collections'
        exclude group: 'commons-lang', module:'commons-lang'
        exclude group: 'javassit', module:'javassist'
        exclude group: 'java-persistence', module:'persistence-api'
        exclude group: 'javassist', module: 'javassist'
    }

    // Required for tag library support
    testCompile 'taglibs:standard:1.1.2'
    testCompile "javax.servlet:jstl:1.1.2"
}

// javaee-web-api has a bad versions of classes we need to compile against
// Just remove it fromt the compile classpath here
configurations {
    testCompile {
        exclude module: "javaee-web-api"
    }
}

compileTestGroovy {
    groovyOptions.listFiles = true
}

def parallelTestConfig = {
    maxParallelForks = 2 // Travis provides only 2 cores
    forkEvery = 30
    jvmArgs = ['-Xmx768M', '-XX:MaxPermSize=192m'] // Travis provides up to 3GB of Memory
}

def excludedTestClasses = ["**/*TestCase.class",
                           "**/*\$*.class"]
def testsToBeExecutedInIsolation = ["**/ContentFormatControllerTests.class",
                                    "**/JSONBindingTests.class",
                                    "**/AutoParams*MarshallingTests.class",
                                    "**/JSONBindingToNullTests.class",
                                    "**/ControllerWithXmlConvertersTests.class",
                                    "**/GroovyPageAttributesTests.class",
                                    "**/BindingExcludeTests.class",
                                    "**/NestedXmlBindingTests.class",
                                    "**/GSPResponseWriterSpec.class",
                                    "**/RespondMethodSpec.class",
                                    "**/ContentNegotiationSpec.class",
                                    "**/pages/ext/jsp/*.class"]

test {
    configure parallelTestConfig
    excludes = ["grails/", "org/grails/web/taglib/", "org/codehaus/"] + testsToBeExecutedInIsolation + excludedTestClasses
}

task execGrailsPackageTests(type: Test) {
    configure parallelTestConfig
    excludes = testsToBeExecutedInIsolation + excludedTestClasses
    includes = ["grails/"]
}

task execOrgGrailsWebTaglibPackageTests(type: Test) {
    configure parallelTestConfig
    excludes = testsToBeExecutedInIsolation + excludedTestClasses
    includes = ["org/grails/web/taglib/"]
}

task execOrgCodehausPackageTests(type: Test) {
    configure parallelTestConfig
    excludes = testsToBeExecutedInIsolation + excludedTestClasses
    includes = ["org/codehaus/"]
}

task execIsolatedTests(type: Test) {
    forkEvery = 1
    excludes = excludedTestClasses
    includes = testsToBeExecutedInIsolation
}

test.dependsOn execGrailsPackageTests, execOrgGrailsWebTaglibPackageTests, execOrgCodehausPackageTests, execIsolatedTests
